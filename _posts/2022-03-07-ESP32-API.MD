---
layout: article
title:  "Arduino ESP32常用接口"
date:   2022-03-07
categories: 硬件开发
tags: [ESP32,硬件开发]
---

## ESP API

ESP ADC:<https://docs.espressif.com/projects/arduino-esp32/en/latest/api/adc.html>
`pin`用于读取模拟值的GPIO引脚

需知开发板支持哪些API，以及开发板的引脚定义。

### Arduino-ESP ADC API

下面是Arduino的ESP API，注意ESP开发板是否属于Arduino开发板系列的，不是请跳过。

#### 软重启
重启两个CPU，重新引导
```C
ESP.restart();
```

#### 模拟读取原始值
```C
analogRead(uint8_t pin);
```

#### 模拟读取电压值
```C
analogReadMilliVolts(uint8_t pin);
```

#### 模拟读取分辨率
该函数用于设置analogRead返回值的分辨率。所有芯片的默认值为 12 位（值0~4096），除了 ESP32S3，默认值为 13 位（值0~8192）。当设置不同的分辨率时，读取的值将被移动以匹配给定的分辨率。
>ESP32分辨率9~12bit
分辨率bits范围为（1~16） 。如果不使用此功能，将使用默认值。
```C
analogReadResolution(uint8_t bits);
```
#### 设置时钟Div
设置ADC的时钟分频器clockDiv范围(1~255)，默认1
```C
analogSetClockDiv(uint8_t clockDiv);
```

#### 设置Attenuation
我的ESP32 Attenuation范围：

Attenuation | 可测量的输入电压范围
--- | ---
ADC_ATTEN_DB_0|100毫伏~950毫伏
ADC_ATTEN_DB_2_5|100 毫伏 ~ 1250 毫伏
ADC_ATTEN_DB_6|150 毫伏 ~ 1750 毫伏
ADC_ATTEN_DB_11|150 毫伏 ~ 2450 毫伏

所有通道的Attenuation设置

```C
analogSetAttenuation(adc_attenuation_t attenuation);
```

特定通道的Attenuation设置
```C
analogSetPinAttenuation(uint8_t pin, adc_attenuation_t attenuation);
```

### ESP32芯片专用ADC API

#### 模拟采样位
设置采样位和分辨率的，默认12位bits，值范围（0~4095），范围9~12位
```C
analogSetWidth(uint8_t bits);
```

#### 设置VRefpin
设置用于ADC校准的引脚，比如ESP引脚25，26或27尚未校准
```C
analogSetVRefPin(uint8_t pin);
```

#### 读取HALL
读取引脚36(SVP) 39(SVN)的HALL传感器的ADC值
```
hallRead();
```

#### 列表
- `analogReadResolution(resolution)`：设置采样位数和分辨率。它可以是 9 (0 – 511) 和 12 位 (0 – 4095) 之间的值。默认为 12 位分辨率。
- `analogSetWidth(width)`：设置采样位数和分辨率。它可以是 9 (0 – 511) 和 12 位 (0 – 4095) 之间的值。默认为 12 位分辨率。
- `analogSetCycles(cycles)`：设置每个样本的循环数。默认值为 8。范围：1 到 255。
- `analogSetSamples(samples)`：设置范围内的样本数。默认为 1 个样本。它具有提高灵敏度的作用。
- `analogSetClockDiv(attenuation)`：设置 ADC 时钟的分频器。默认值为 1。范围：1 到 255。
- `analogSetAttenuation(attenuation)`：设置所有 ADC 引脚的输入衰减。默认为`ADC_11db`. 接受的值：
	- `ADC_0db`: 不设置衰减。ADC 可测量高达约 800 mV（1V 输入 = ADC 读数为 1088）。
	- `ADC_2_5db`: ADC 的输入电压将被衰减，将测量范围扩大到大约 1100 毫伏。（1V 输入 = 3722 的 ADC 读数）。
	- `ADC_6db`: ADC 的输入电压将被衰减，将测量范围扩大到大约 1350 毫伏。（1V 输入 = 3033 的 ADC 读数）。
	- `ADC_11db`: ADC 的输入电压将被衰减，将测量范围扩大到大约 2600 毫伏。（1V 输入 = 1575 的 ADC 读数）。
- `analogSetPinAttenuation(pin, attenuation)`：设置指定引脚的输入衰减。默认是ADC_11db. 衰减值与之前的功能相同。
- `adcAttachPin(pin)`：将引脚连接到 ADC（也清除任何其他可能打开的模拟模式）。返回 TRUE 或 FALSE 结果。
- `adcStart(pin)`,`adcBusy(pin)`和`resultadcEnd(pin)`：在附加引脚的总线上启动 ADC 转换。检查引脚的 ADC 总线上的转换当前是否正在运行（返回 TRUE 或 FALSE）。获取转换结果：返回 16 位整数。

#### 示例
关于ESP32-DEVKITV1示例代码的参考资料：<https://randomnerdtutorials.com/esp32-adc-analog-read-arduino-ide/>
```C
void setup() {
  // initialize serial communication at 115200 bits per second:
  Serial.begin(115200);
  
  //set the resolution to 12 bits (0-4096)
  analogReadResolution(12);
}

void loop() {
  // read the analog / millivolts value for pin 2:
  int analogValue = analogRead(2);
  int analogVolts = analogReadMilliVolts(2);
  
  // print out the values you read:
  Serial.printf("ADC analog value = %d\n",analogValue);
  Serial.printf("ADC millivolts value = %d\n",analogVolts);
  
  delay(100);  // delay in between reads for clear read from serial
}
```

### 通用输入输出接口

#### 引脚模式
```C
pinMode(uint8_t pin, uint8_t mode);
```
- pin 定义 GPIO 引脚号。

- mode 设置操作模式。

输入输出支持的模式：
- INPUT将 GPIO 设置为没有上拉或下拉（高阻抗）的输入。

- OUTPUT将 GPIO 设置为输出/读取模式。

- INPUT_PULLDOWN使用内部下拉将 GPIO 设置为输入。

- INPUT_PULLUP使用内部上拉将 GPIO 设置为输入。

#### 数值写入
该函数`digitalWrite`将所选 GPIO 的状态设置为`HIGH`或`LOW`。此功能仅`pinMode`在配置为`OUTPUT`时使用
```C
digitalWrite(uint8_t pin, uint8_t val);
```

#### 数值读取
该函数`digitalRead`读取两种状态（false和true），仅`pinMode`在配置为`INPUT`时使用
```C
digitalRead(uint8_t pin);
```

#### 示例
```C
#define LED    12
#define BUTTON 2

uint8_t stateLED = 0;

  void setup() {
      pinMode(LED, OUTPUT);
      pinMode(BUTTON,INPUT_PULLUP);
  }

  void loop() {

     if(!digitalRead(BUTTON)){
       stateLED = stateLED^1;
      digitalWrite(LED,stateLED);
    }
  }
```

### ESP LED API
LED API参考链接:<https://docs.espressif.com/projects/arduino-esp32/en/latest/api/ledc.html>

#### 示例
ESP外接二极灯管
效果是淡入淡出
```C
/*
 LEDC Software Fade

 This example shows how to software fade LED
 using the ledcWrite function.

 Code adapted from original Arduino Fade example:
 https://www.arduino.cc/en/Tutorial/Fade

 This example code is in the public domain.
 */

// use first channel of 16 channels (started from zero)
#define LEDC_CHANNEL_0     0

// use 12 bit precission for LEDC timer
#define LEDC_TIMER_12_BIT  12

// use 5000 Hz as a LEDC base frequency
#define LEDC_BASE_FREQ     5000

// fade LED PIN (replace with LED_BUILTIN constant for built-in LED)
#define LED_PIN            5

int brightness = 0;    // how bright the LED is
int fadeAmount = 5;    // how many points to fade the LED by

// Arduino like analogWrite
// value has to be between 0 and valueMax
void ledcAnalogWrite(uint8_t channel, uint32_t value, uint32_t valueMax = 255) {
  // calculate duty, 4095 from 2 ^ 12 - 1
  uint32_t duty = (4095 / valueMax) * min(value, valueMax);

  // write duty to LEDC
  ledcWrite(channel, duty);
}

void setup() {
  // Setup timer and attach timer to a led pin
  ledcSetup(LEDC_CHANNEL_0, LEDC_BASE_FREQ, LEDC_TIMER_12_BIT);
  ledcAttachPin(LED_PIN, LEDC_CHANNEL_0);
}

void loop() {
  // set the brightness on LEDC channel 0
  ledcAnalogWrite(LEDC_CHANNEL_0, brightness);

  // change the brightness for next time through the loop:
  brightness = brightness + fadeAmount;

  // reverse the direction of the fading at the ends of the fade:
  if (brightness <= 0 || brightness >= 255) {
    fadeAmount = -fadeAmount;
  }
  // wait for 30 milliseconds to see the dimming effect
  delay(30);
}
```
显示RGB颜色
```C
/*
  ledcWrite_RGB.ino
  Runs through the full 255 color spectrum for an rgb led 
  Demonstrate ledcWrite functionality for driving leds with PWM on ESP32
 
  This example code is in the public domain.
  
  Some basic modifications were made by vseven, mostly commenting.
 */
 
// Set up the rgb led names
uint8_t ledR = 2;
uint8_t ledG = 4;
uint8_t ledB = 5; 

uint8_t ledArray[3] = {1, 2, 3}; // three led channels

const boolean invert = true; // set true if common anode, false if common cathode

uint8_t color = 0;          // a value from 0 to 255 representing the hue
uint32_t R, G, B;           // the Red Green and Blue color components
uint8_t brightness = 255;  // 255 is maximum brightness, but can be changed.  Might need 256 for common anode to fully turn off.

// the setup routine runs once when you press reset:
void setup() 
{            
  Serial.begin(115200);
  delay(10); 
  
  ledcAttachPin(ledR, 1); // assign RGB led pins to channels
  ledcAttachPin(ledG, 2);
  ledcAttachPin(ledB, 3);
  
  // Initialize channels 
  // channels 0-15, resolution 1-16 bits, freq limits depend on resolution
  // ledcSetup(uint8_t channel, uint32_t freq, uint8_t resolution_bits);
  ledcSetup(1, 12000, 8); // 12 kHz PWM, 8-bit resolution
  ledcSetup(2, 12000, 8);
  ledcSetup(3, 12000, 8);
}

// void loop runs over and over again
void loop() 
{
  Serial.println("Send all LEDs a 255 and wait 2 seconds.");
  // If your RGB LED turns off instead of on here you should check if the LED is common anode or cathode.
  // If it doesn't fully turn off and is common anode try using 256.
  ledcWrite(1, 255);
  ledcWrite(2, 255);
  ledcWrite(3, 255);
  delay(2000);
  Serial.println("Send all LEDs a 0 and wait 2 seconds.");
  ledcWrite(1, 0);
  ledcWrite(2, 0);
  ledcWrite(3, 0);
  delay(2000);
 
  Serial.println("Starting color fade loop.");
  
 for (color = 0; color < 255; color++) { // Slew through the color spectrum

  hueToRGB(color, brightness);  // call function to convert hue to RGB

  // write the RGB values to the pins
  ledcWrite(1, R); // write red component to channel 1, etc.
  ledcWrite(2, G);   
  ledcWrite(3, B); 
 
  delay(100); // full cycle of rgb over 256 colors takes 26 seconds
 }
 
}

// Courtesy http://www.instructables.com/id/How-to-Use-an-RGB-LED/?ALLSTEPS
// function to convert a color to its Red, Green, and Blue components.

void hueToRGB(uint8_t hue, uint8_t brightness)
{
    uint16_t scaledHue = (hue * 6);
    uint8_t segment = scaledHue / 256; // segment 0 to 5 around the
                                            // color wheel
    uint16_t segmentOffset =
      scaledHue - (segment * 256); // position within the segment

    uint8_t complement = 0;
    uint16_t prev = (brightness * ( 255 -  segmentOffset)) / 256;
    uint16_t next = (brightness *  segmentOffset) / 256;

    if(invert)
    {
      brightness = 255 - brightness;
      complement = 255;
      prev = 255 - prev;
      next = 255 - next;
    }

    switch(segment ) {
    case 0:      // red
        R = brightness;
        G = next;
        B = complement;
    break;
    case 1:     // yellow
        R = prev;
        G = brightness;
        B = complement;
    break;
    case 2:     // green
        R = complement;
        G = brightness;
        B = next;
    break;
    case 3:    // cyan
        R = complement;
        G = prev;
        B = brightness;
    break;
    case 4:    // blue
        R = next;
        G = complement;
        B = brightness;
    break;
   case 5:      // magenta
    default:
        R = brightness;
        G = complement;
        B = prev;
    break;
    }
}
```